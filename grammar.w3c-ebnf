/* This grammar definition uses the W3C EBNF syntax. */
/* See: https://www.w3.org/TR/2008/REC-xml-20081126/#sec-notation */

Program ::= (ImportStmt | ExportStmt | Definition)*

ImportStmt ::= "import" IdentBindings "from" StringLit ";"

ExportStmt ::= "export" (Definition | IdentBindings) ";"

IdentBindings ::= IdentBinding ("," IdentBinding)*

IdentBinding ::= Ident | Ident "~" Ident

Definition ::= TypeDef | FnDef | OnDef

TypeDef ::= "type" Ident GenTypeListDecl? [=~] Type ";"

FnDef ::= "fn" Ident GenTypeListDecl? "(" FieldDef? ")" ":" Type BlockExpr

OnDef ::= "on" Family "{" AssociatedDef* "}"

AssociatedDef ::= TypeDef | ("static" | "mut")? FnDef

GenTypeListDecl ::= "<" Ident ("," Ident)* "," ">"

Expr ::=
  | Ident
  | BlockExpr
  | IfExpr
  | WhileExpr
  | ForExpr
  | LoopExpr
  | MatchExpr
  | FnCallExpr
  | BinopExpr
  | UnopExpr
  | LitExpr
  | "(" ExprList? ")"

BlockExpr ::= "{" (Stmt ";")* Expr? "}"

IfExpr ::= "if" Expr BlockExpr ("else" "if" BlockExpr)* ("else" Expr)?

WhileExpr ::= "while" Expr BlockExpr

ForExpr ::= "for" Stmt "," Expr "," Expr BlockExpr

LoopExpr ::= "loop" BlockExpr

MatchExpr ::= "match" Expr "{" MatchArm+ "}"

MatchArm ::= Pattern "=>" Expr

FnCallExpr ::= Expr "(" ExprList? ")"

ExprList ::= Expr ("," Expr)* ","?

// TODO: Make precedence explicit in the grammar.
BinopExpr ::=
  | Expr "|" Expr
  | Expr "&" Expr
  | Expr "^" Expr
  | Expr "<<" Expr
  | Expr ">>" Expr
  | Expr "==" Expr
  | Expr "!=" Expr
  | Expr "<=" Expr
  | Expr ">=" Expr
  | Expr "<" Expr
  | Expr ">" Expr
  | Expr "+" Expr
  | Expr "-" Expr
  | Expr "*" Expr
  | Expr "/" Expr
  | Expr "%" Expr
  | Expr "&&" Expr
  | Expr "||" Expr
  | Expr "=" Expr
  | Expr "." Expr

UnopExpr ::=
  | "!" Expr
  | "-" Expr
  | "~" Expr
  | "*" Expr
  | "&" Expr

LitExpr ::=
  | "null"
  | "true"
  | "false"
  | DecimalLit
  | HexLit
  | FloatLit
  | StringLit
  | CharLit

Stmt ::=
  | LetStmt
  | Expr
  | "return" Expr?
  | "break" Expr?
  | "continue"

LetStmt ::= "let" Pattern (":" Type)? "=" Expr

Pattern ::=
  | "_"
  | Ident
  | "mut" Ident
  | "(" ")"
  | "(" PatternList ")"
  | Ident "(" PatternList ("," ".."?)? ")"
  | Ident "{" PatternBindings  ("," ".."?)? "}"
  | Ident "@" Pattern

PatternList ::= Pattern ("," Pattern)*

PatternBindings ::= PatternBinding ("," PatternBinding)*

PatternBinding ::= Ident ":" Pattern | "mut"? Ident

Type ::=
  | TypeName
  | "(" TypeList? ")"
  | "struct" "{" FieldDefs "}"
  | "enum" "{" EnumCases "}"
  | "*" Type

TypeName ::= Primitive | Ident ("<" TypeList ">")?

FieldDefs ::= FieldDef ("," FieldDef)* ","?

FieldDef ::= Ident ":" Type

EnumCases ::= EnumCase ("," EnumCase)* ","?

EnumCase ::=
  | Ident
  | Ident "(" TypeList ")"
  | Ident "{" FieldDefs "}"

TypeList ::= Type ("," Type)* ","?

Family ::=
  | FamilyName
  | "(" FamilyList? ")"
  | "struct" "{" FamilyFieldDefs "}"
  | "enum" "{" EnumFamilyCases "}"
  | "*" Family
  | Family "~" Ident

FamilyName ::= Primitive | Ident ("<" FamilyList ">")?

FamilyFieldDefs ::= FamilyFieldDef ("," FamilyFieldDef)* ","?

FamilyFieldDef ::= Ident ":" Family

EnumFamilyCases ::= EnumFamilyCase ("," EnumFamilyCase)* ","?

EnumFamilyCase ::=
  | Ident
  | Ident "(" FamilyList ")"
  | Ident "{" FamilyFieldDefs "}"

FamilyList ::= Family ("," Family)* ","?

Primitive ::=
  | "bool"
  | "u8"
  | "u16"
  | "u32"
  | "u64"
  | "i8"
  | "i16"
  | "i32"
  | "i64"
  | "f32"
  | "f64"

DecimalLit ::= Digit+

HexLit ::= "0x" HexDigit+

FloatLit ::=
  | Digit+ "." Digit+
  | Digit+ "."
  | "." Digit+

StringLit ::= '"' StringElem* '"'

StringElem ::= [^"] | EscapeSeq

CharLit ::= "'" CharElem "'"

CharElem ::= [^'] | EscapeSeq

EscapeSeq ::=
  | "\\\\"
  | '\\"'
  | "\\'"
  | "\\0"
  | "\\n"
  | "\\r"
  | "\\t"
  | "\\b"
  | "\\u" HexDigit HexDigit HexDigit HexDigit

Digit ::= [0-9]

HexDigit ::= [0-9a-fA-F]

Ident ::= ([a-zA-Z_][a-zA-Z0-9_]*) - Keyword

Keyword ::=
  | Primitive
  | "import"
  | "from"
  | "export"
  | "type"
  | "struct"
  | "enum"
  | "static"
  | "fn"
  | "on"
  | "self"
  | "mut"
  | "let"
  | "if"
  | "else"
  | "while"
  | "for"
  | "loop"
  | "match"
  | "return"
  | "break"
  | "continue"
  | "null"
  | "true"
  | "false"
